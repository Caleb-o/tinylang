namespace Game {
	let FREE = 0;
	let BOMB = 1;

	let BOMB_CHANCE = 20;

	class Tile {
		var state;
		var open;
		var flagged;
		var neighbours;

		function Tile(state) {
			self.state = state;
			self.open = false;
			self.flagged = false;
			self.neighbours = 0;
		}
	}

	class Field {
		var done;
		var first;
		var bombs;
		var width;
		var height;
		var tiles;
		var cursor_x;
		var cursor_y;

		function Field(width, height) {
			self.done = false;
			self.first = true;
			self.bombs = 0;

			self.width = width;
			self.height = height;
			self.tiles = [];

			self.cursor_x = 0;
			self.cursor_y = 0;

			while var idx = 0; idx < width * height; idx = idx + 1 {
				self.tiles = self.tiles + [Tile(FREE)];
			}
		}

		function initialise() {
			while var idx = 0; idx < self.width * self.height; idx = idx + 1 {
				if !self.tiles[idx].open {
					var state = FREE;

					if builtin.rand_range(0, 100) < BOMB_CHANCE {
						state = BOMB;
						self.bombs = self.bombs + 1;
					}

					self.tiles[idx].state = state;
				}
			}
		}

		function get_state_of(index) {
			let t = self.tiles[index];

			if !t.open {
				if t.flagged {
					return "?";	
				}
				return ".";
			} else {
				match t.state {
					FREE => {
						if t.neighbours == 0 {
							return " ";
						}
						return builtin.to_string(t.neighbours);
					};
					BOMB => return "*";
				}
			}

			throw "Invalid state";
		}

		function find_neighbours(x, y) {
			var bombCount = 0;

			let check = function(x, y) {
				# Disallow wrapping, this messes with the count
				if x < 0 || x >= self.width {
					return;
				}

				# Disallow wrapping, this messes with the count
				if y < 0 || y >= self.height {
					return;
				}

				if self.tiles[x + self.width * y].state == BOMB {
					bombCount = bombCount + 1;
				}
			};

			check(x - 1, y);
			check(x + 1, y);
			check(x, y - 1);
			check(x, y + 1);
			check(x - 1, y - 1);
			check(x - 1, y + 1);
			check(x + 1, y - 1);
			check(x + 1, y + 1);

			return bombCount;
		}

		function set_flag() {
			let t = self.tiles[self.cursor_x + self.width * self.cursor_y];

			if !t.open {
				t.flagged = !t.flagged;
			}
		}

		function open_free(x, y) {
			if self.tiles[x + self.width * y].state == FREE {
				let check = function(x, y) {
					# Disallow wrapping, this messes with the count
					if x < 0 || x >= self.width {
						return;
					}

					# Disallow wrapping, this messes with the count
					if y < 0 || y >= self.height {
						return;
					}

					let index = x + self.width * y;
					if self.tiles[index].open {
						return;
					}

					if self.tiles[index].state == BOMB {
						return;
					}

					# Don't open tiles with nearby bombs
					if self.find_neighbours(x, y) > 0 {
						return;
					}

					# Can just set the field, since we check for no neighbours and bombs
					self.tiles[x + self.width * y].open = true;
					self.open_free(x, y);
				};

				check(x - 1, y);
				check(x + 1, y);
				check(x, y - 1);
				check(x, y + 1);
				check(x - 1, y - 1);
				check(x - 1, y + 1);
				check(x + 1, y - 1);
				check(x + 1, y + 1);
			}
		}

		function open() {
			let t = self.tiles[self.cursor_x + self.width * self.cursor_y];

			if !t.open {
				t.open = true;

				if t.state == BOMB {
					self.end("Game Over!");
					return;
				}

				t.neighbours = self.find_neighbours(self.cursor_x, self.cursor_y);
				if !self.first && t.neighbours == 0 {
					self.open_free(self.cursor_x, self.cursor_y);
				}
			}

			# Initialise the field after the first attempt
			if self.first {
				self.first = false;
				self.initialise();
			}
		}

		function open_all() {
			while var idx = 0; idx < self.width * self.height; idx = idx + 1 {
				let t = self.tiles[idx];

				if t.state == BOMB {
					t.open = true;
				}
			}
		}

		function end(message) {
			self.done = true;
			builtin.reset();
			self.open_all();
			self.render();

			print(message);
			builtin.sleep(4000);
		}

		function run() {
			while !self.done {
				self.render();

				print("wasd - Move | f - Flag | <ENTER> - Open | e - Exit | r - Reveal");
				builtin.out(">> ");
				let input = builtin.read_line();

				match input {
					"e" => {
						self.end("Goodbye!");
						return;
					};

					"w" => {
						if self.cursor_y - 1 >= 0 {
							self.cursor_y = self.cursor_y - 1;
						}
					};

					"s" => {
						if self.cursor_y + 1 < self.height {
							self.cursor_y = self.cursor_y + 1;
						}
					};

					"a" => {
						if self.cursor_x - 1 >= 0 {
							self.cursor_x = self.cursor_x - 1;
						}
					};

					"d" => {
						if self.cursor_x + 1 < self.width {
							self.cursor_x = self.cursor_x + 1;
						}
					};

					"" => self.open();
					"f" => self.set_flag();
					"r" => self.open_all();
				}

				builtin.reset();
			}
		}

		function render_line() {
			builtin.out("--");
			while var idx = 0; idx < self.width; idx = idx + 1 {
				builtin.out("---");
			}
			builtin.out("--");
			print();
		}

		function render() {
			if self.first {
				print("### MineSweeper ###");
			} else {
				print("### MineSweeper : Bombs ", self.bombs, " ###");
			}

			self.render_line();

			builtin.out("| ");
			while var idx = 0; idx < self.width * self.height; idx = idx + 1 {
				if idx > 0 && builtin.mod(idx, self.width) == 0 {
					builtin.out(" |");
					print();
					builtin.out("| ");
				}

				let x = builtin.mod(idx, self.width);
				let y = idx / self.width;

				if x == self.cursor_x && y == self.cursor_y {
					builtin.out("[" + self.get_state_of(idx) + "]");
				} else {
					builtin.out(" " + self.get_state_of(idx) + " ");
				}
			}
			print(" |");

			self.render_line();

			print();
			print();
		}
	}
}

namespace Menu {
	class Item {
		var name;
		var item;

		function Item(name, item) {
			self.name = name;
			self.item = item;
		}
	}


	class Main {
		var running;
		var idx;
		var items;

		function Main(items) {
			self.running = true;
			self.idx = 0;
			self.items = items;
		}

		function run() {
			while self.running {
				self.render();

				match builtin.read_line() {
					"w" => {
						if self.idx - 1 >= 0 {
							self.idx = self.idx - 1;
						}
					};

					"s" => {
						if self.idx + 1 < builtin.len(self.items) {
							self.idx = self.idx + 1;
						}
					};

					"" => {
						self.items[self.idx].item();
						return;
					};
				}

				builtin.reset();
			}
		}

		function render() {
			print("Select Field Size");
			while var idx = 0; idx < builtin.len(self.items); idx = idx + 1 {
				if self.idx == idx {
					builtin.out("> ");
				} else {
					builtin.out("  ");
				}

				print(idx + 1, ". ", self.items[idx].name);
			}

			print();
			print("w - up | s - down");
		}
	}
}

# Initialise with random seed
builtin.rand_seed_init();
var field = ();

let menu = Menu.Main(
	[
		Menu.Item("8x8", function() { field = Game.Field(8, 8); }),
		Menu.Item("16x16", function() { field = Game.Field(16, 16); }),
		Menu.Item("24x24", function() { field = Game.Field(24, 24); }),
		Menu.Item("Exit", function() {}),
	]
);

menu.run();
builtin.reset();

# Run the game
if !builtin.is_unit(field) {
	field.run();
	builtin.reset();
}
