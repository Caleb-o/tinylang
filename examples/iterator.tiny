class Iterator {
	var idx;
	var length;
	var arr;

	function Iterator(arr) {
		builtin.assertm(
			builtin.type_name(arr) == "list" ||
			builtin.type_name(arr) == "string",
			"Iterators can only be used on lists and strings"
		);
		self.idx = 0;
		self.length = builtin.len(arr);
		self.arr = arr;
	}

	function next() {
		self.idx = self.idx + 1;
		if self.idx < self.length {
			return self.arr[self.idx-1];
		}
		return self.arr[self.length-1];
	}

	function done() {
		return self.idx >= self.length;
	}

	function refresh() {
		self.idx = 0;
		self.length = builtin.len(self.arr);
	}

	function map(fn) {
		self.refresh();
		var values = [];

		while !self.done() {
			let element = self.next();
			values = values + [fn(element)];
		}

		self.arr = values;

		return self;
	}

	function filter(fn) {
		self.refresh();
		var values = [];

		while !self.done() {
			let element = self.next();
			let result = fn(element);

			builtin.assertm(builtin.type_name(result) == "bool", "Filter predicate must return a boolean");

			if result {
				values = values + [element];
			}
		}

		self.arr = values;

		return self;
	}

	function rev() {
		self.refresh();
		var values = [];

		while !self.done() {
			let element = self.next();
			values = [element] + values;
		}

		self.arr = values;

		return self;
	}

	function collect() {
		return self.arr;
	}
}

class Person {
	var name;
	var age;

	function Person(name, age) {
		self.name = name;
		self.age = age;
	}
}


let list = [1, 2, 3, 4, 5, 6, 7, 8];
let list2 = Iterator(list)
				.map(function(item) { return item * 3; })
				.filter(function(item) { return builtin.mod(item, 2) == 0; })
				.rev()
				.collect();

print(list2);


let plist = [
	Person("Bob", 42),
	Person("Dave", 21),
	Person("Dingle", 8),
	Person("Bill", 36),
	Person("Tyler", 14),
	Person("Tim", 18),
	Person("John", 83),
	Person("Pat", 5),
];

let children = Iterator(plist)
				.filter(function(item) { return item.age > 25; })
				.collect();

print(children);


let words = Iterator(["Hello", "Cat", "Dog", "Bread", "Cake", "Can", "Chicken", "Whale", "Cow"])
				.filter(function(item) { return builtin.len(item) == 3; })
				.collect();

print(words);